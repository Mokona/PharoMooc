{
	"title":"Class methods At Work"
}

${slide:title=What you Will Learn}$

- Class methods are dynamically looked up
- Act as registration mechanism
- No need for factory as in Java

${slide:title=Parsing Lines}$

Imagine we want to parse
[[[
!Section Title
- list item
-- sub-list item

Any text here
]]]


${slide:title=A Possible Design}$

+>file://figures/items.png|width=80+

- Document item ""classes"" know
-- if they can parse a line (==canParse:==)
-- how to create instances (==newFromLine:==)

${slide:title=Parsing Lines}$

[[[
Parser>>parse: line
   self documentClasses
      detect: [ :subclass |
         (subclass canParse: aLine)
            ifTrue: [ ^ subclass newFromLine: line ] ]

Parser>>documentClasses
   ^ DocumentItem allSubclasses
      sorted: [ :cls1 :cls2 | cls1 priority < cls2 priority ]
]]]


${slide:title=What do we See}$

- ==documentClasses== sorts the subclasses
- subclasses specialize ==canParse:== and ==newFromLine:==

${slide:title=Another Example}$

Inside Pharo the way command-lines are managed use the same approach

[[[
CommandLineHandler class>>allHandlers
   ^ self allSubclasses
        reject: [ :handler| handler isAbstract ]

CommandLineHandler class>>handlersFor: aCommandLine
  ^ self allHandlers
       select: [ :handlerClass |
          handlerClass isResponsibleFor: aCommandLine ]
]]]

${slide:title=CommandLineHandler and subclasses}$

[[[
CommandLineHandler class >>	isResponsibleFor: aCommandLineArguments
	^ aCommandLineArguments includesSubCommand: self commandName		
]]]

[[[
EvaluateCommandLineHandler class >>	isResponsibleFor: commandLineArguments
  "directly handle top-level -e and --evaluate options"
  commandLineArguments withFirstArgument: [ :arg | 
    (#('-e' '--evaluate') includes: arg)
        ifTrue: [ ^ true ]].
  ^ commandLineArguments includesSubCommand: self commandName
]]]


${slide:title=Conclusion}$

- Messages sent to classes are resolved dynamically the same way as messages sent to instances
- Classes can then be used as registration mechanism
- Each subclass defines a behavior and the condition to use it

% Local Variables:
% compile-command: "cd ../.. && ./compile.sh --to=Beamer Slides/Week3/W3S8-ClassMethodsAtWork.pillar"
% End:
