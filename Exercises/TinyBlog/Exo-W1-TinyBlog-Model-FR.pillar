{
    "title":"L'application TinyBlog : présentation et modèle"
}

!L'application TinyBlog

Tout au long de ce MOOC, nous allons vous guider pour développer et enrichir une application Web  pour gérer un ou plusieurs blogs. 
Dans la suite appellerons cette application: TinyBlog.
L'idée est qu'un visiteur du site Web puisse voir les posts et que l'auteur du blog puisse se connecter sur le site pour administrer le blog c'est-à-dire ajouter, supprimer ou modifier des posts.

TinyBlog est une application à but pédagogique qui va vous montrer comment définir et déployer une application en utilisant Pharo/Seaside/Mongo ainsi que des frameworks disponibles en Pharo comme NeoJSON. Nous exposerons aussi comment exposer votre application via un serveur REST. 
Les solutions proposées dans ce tutoriel sont parfois non optimales afin de vous faire réagir et que vous puissiez proposer d'autres solutions et des améliorations.
Notre objectif n'est pas d'être exhaustif. Nous montrons une façon de faire cependant nous invitons le lecteur à lire les références sur les autres chapitres, livres et tutoriaux Pharo afin d'approfondir son expertise et enrichir son application.

!Installation de Pharo

Vous pouvez télécharger Pharo (machine virtuelle et image) à l'adresse : *http://mooc.pharo.org/*.

Attention, dans ce MOOC nous supposons que vous utiliser Pharo 5.0.
Nous allons également utiliser des bibliothèques et frameworks supplémentaires pour le développement d'applications Web: Seaside, Magritte, Bootstrap, Voyage, VoyageMongo, ...

Il existe une image spécifique contenant déjà les bibliothèques courantes nécessaires au développement d'applications Web. 
Vous pouvez la télécharger directement en utilisant Pharo Launcher ou depuis *http://mooc.pharo.org/*.

% ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
!Le modèle des posts
% ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Le modèle de TinyBlog est extrêmement simple.
Nous commencerons ici par la classe ==TBPost==.

!!La classe TBPost

Dans la suite, nous préfixons tous les noms de classe par ==TB== (pour TinyBlog).
Il serait préférable que choisissiez un autre préfixe (par exemple ==TBM==) afin de pouvoir ensuite charger la correction dans la même image Pharo et la comparer à votre propre classe.

Définissez la classe ==TBPost== comme suit:

[[[
Object subclass: #TBPost
   instanceVariableNames: 'title text date category visible'
   classVariableNames: ''
   package: 'TinyBlog'
]]]

!!Description d'un Post

Nous utilisons cinq variables d'instance pour décrire un post sur le blog.

|!Variable |!Signification |
|title         |Titre du post                |
|text          |Texte du post                |
|date          |Date de redaction            |
|category      |Rubrique contenant le post   |
|visible       |Post visible ou pas ?        |


Ces variables ont des méthodes accesseurs dans le protocole 'accessing'.

[[[
TBPost >> title
   ^ title

TBPost >> title: anObject
   title := anObject

TBPost >> text
   ^ text

TBPost >> text: anObject
   text := anObject

TBPost >> date
   ^ date

TBPost >> date: anObject
   date := anObject

TBPost >> visible
   ^ visible

TBPost >> visible: anObject
   visible := anObject

TBPost >> category
   ^ category

TBPost >> category: anObject
   category := anObject
]]]

!!Gérer la visibilité d'un post

Il faut avoir la possibilité d'indiquer qu'un post est visible ou pas. Il faut également pouvoir demander à un post s'il est visible. Les méthodes sont définies dans la protocole 'action'.

[[[
TBPost >> beVisible
   self visible: true

TBPost >> notVisible
   self visible: false

TBPost >> isVisible
   ^ self visible
]]]


!!Initialisation

La méthode ==initialize== (protocole 'initialization') fixe la date à celle du jour et la visibilité à faux. L'utilisateur devra par la suite activer la visibilité ce qui permet de rédiger des brouillons et de publier lorsque le post est terminé. Un post est également rangé par défaut dans la catégorie 'Unclassified' que l'on définit au niveau classe.  La méthode ==unclassifiedTag== renvoie une valeur indiquant que le post n'est pas rangé dans une catégorie.

[[[
TBPost class >> unclassifiedTag
   ^ 'Unclassified'
]]]

Attention la méthode ==unclassifiedTag== est définie au niveau de la classe (cliquer le bouton classe pour la définir). Les autres méthodes sont des méthodes d'instances c'est-à-dire qu'elles seront exécutées sur des instances de la classe ==TBPost==. 

[[[
TBPost >> initialize
   self category: self class unclassifiedTag.
   self date: Date today.
   self notVisible
]]]

Dans la solution proposée ci-dessus pour la méthode ==initialize==, il serait préférable de ne pas faire une référence en dur à la classe ==TBPost==. 
Nous traiterons ce point ultérieurement dans le MOOC.


!!Méthodes de création

Coté classe, on spécifie des méthodes pour faciliter la création de post appartenant ou pas à une catégorie.

[[[
TBPost class >> title: aTitle text: aText
   ^ self new
        title: aTitle;
        text: aText;
        yourself
]]]

[[[
TBPost class >> title: aTitle text: aText category: aCategory
   ^ (self title: aTitle text: aText)
            category: aCategory;
            yourself
]]]


!!Créer des posts

Vous pouvez dès maintenant créer des posts.
Ouvrez l'outil Playground et exécuter l'expression suivante :

[[[
TBPost 
	title: 'Welcome in TinyBlog' 
	text: 'TinyBlog is a small blog engine made with Pharo.' 
	category: 'TinyBlog'
]]]

Si vous inspectez le code ci-dessus (clique droit sur l'expression et "inspect"), vous allez obtenir un inspecteur sur l'objet post nouvellement créé.

!!Test

Nous définissons une méthode afin de savoir si un post est classé dans une catégorie.

[[[
TBPost >> isUnclassified
   ^ self category = TBPost unclassifiedTag
]]]

De même il serait préférable de ne pas faire une référence en dur à la classe ==TBPost==.



% !Le modèle des auteurs
%
% Un post est écrit par un auteur.
% Ajouter maintenant une classe nommée ==TBAuthor== sachant qu'un auteur est caractérisé par son pseudo (affiché publiquement dans le post), son login et son mot de passe.
%
% De même que pour les posts, ajoutez des accesseurs et des méthodes de création côté classe.
% Nous souhaitons pouvoir écrire dans un playground :
%
% [[[
% 	luc := TBAuthor pseudo: 'LucF' login: 'luc' password: 'topsecret'
% ]]]
%
% Ajouter un champs ==author== dans la classe ==TBPost== ainsi que des accesseurs.
% Dans un playground, on souhaite pouvoir écrire :
%
% [[[
% 	|luc|
% 	luc := TBAuthor pseudo: 'LucF' login: 'luc' password: 'topsecret'.
% 	(TBPost
% 		title: 'Welcome in TinyBlog'
% 		text: 'TinyBlog is a small blog engine made with Pharo.'
% 		category: 'TinyBlog')
% 		author: luc ;
% 		yourself
% ]]]

